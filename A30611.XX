	.TITLE	MASS
	.RADIX	16
	.ASECT
MAXOB	=0A
CODE:	.BLKB MAXOB
OCD:	.BLKB MAXOB
MODE:	.BLKB MAXOB
TOG:	.BLKB 1
TARG:	.BLKB 1
SCRH:	.BLKB 2
LNUM:	.BLKB 1
HPOS:	.BLKB MAXOB
VPOS:	.BLKB MAXOB
CODET:	.BLKB MAXOB
MODET:	.BLKB MAXOB
HPOST:	.BLKB MAXOB
VPOST:	.BLKB MAXOB
OCDT:	.BLKB MAXOB
VPOSL:	.BLKB MAXOB
TUP:	.BLKB 1
COINS:	.BLKB 1
CREDIT:	.BLKB 1
POLY:	.BLKB 1
FRAME:	.BLKB 2
HNUM:	.BLKB 1
ATRK:	.BLKB 1
TEMP1:	.BLKB 2
TEMP2:	.BLKB 2
TEMP3:	.BLKB 2
TEMP4:	.BLKB 2
DONE:	.BLKB 1
TIME:	.BLKB 1
HEATS:	.BLKB 1
AMT:	.BLKB 2
VALU:	.BLKB 1
LCNT:	.BLKB 1
CNT:	.BLKB 1
POS:	.BLKB 2
LIM:	.BLKB 1
SCORE:	.BLKB 2
PTR:	.BLKB 2
TEMP5:	.BLKB 2
TEMP6:	.BLKB 2
PFLG:	.BLKB 1
SCR2:	.BLKB 2
TIMER:	.BLKB 2
SCRT:	.BLKB 2
EPLY:	.BLKB 2
EFLG:	.BLKB 2
SNUM:	.BLKB 1
RTEST:	.BLKB 2
PIK:	.BLKB 3
LOBJ:	.BLKB 1
SFLG0:	.BLKB 1
SFLG1:  .BLKB 1
SFLG2:	.BLKB 1
SFLG3:	.BLKB 1
SFLG4:	.BLKB 1
TEMP7:	.BLKB 2
KOIN1:  .BLKB 2
FFLG:	.BLKB 2
LOBJS:	.BLKB 1
LNUMS:	.BLKB 1
EFLAG:  .BLKB 2
ARR:	.BLKB 32.
	.=100
SAVR1:	.BLKB 3*MAXOB+5
SAVR2:	.BLKB 3*MAXOB+5
SAVS1:	.BLKB 34.
SAVS2:	.BLKB 34.
SAVM:	.BLKB 32.
PAD1	=7776.
PAD2	=7008.
PAD3	=6240.
PAD4	=5472.
PAD5	=4704.
PAD6	=3936.
TEST	=3000
TESTV	=0AA
MAXOB	=0A
SB	=2001
SW	=2000
HCON	=2002
SOUND1	=4004
SOUND2	=4005
SOUND3	=4006
SLITE	=4007
SCON	=5000
ATR	=4001
CLITE	=4000
CLITE2	=4003
VINT	=4002
AMODE	=2001
CSW	=2000
COINAD	=2001
TREST	=3000
	.SBTTL	POWER ON LOGIC
	.ASECT
	.=6800
PWRON:	SEI			;POWER ON,LOCKOUT IRQ
	CLD			;CLEAR DECIMAL MODE
	LDX	I,0FF		;SET UP STACK
	TXS
	LDA	A,TEST		;PRODUCTION DIAGNOSTIC PRESENT?
	CMP	I,TESTV
	BNE	BEGIN		;NO,START PROGRAM
	JMP	TEST		;YES, GO TO DIAGNOSTIC
BEGIN:	JSR	SETUP		;DO POWER ON INITIALIZATION
MAIN:	JSR	ATRACT
	JSR	START
	JMP	MAIN
	.SBTTL	ATTRACT ROUTINE
ATRACT:	LDA	I,0
	STA	ATRK
	LDA	I,1
	STA	A,ATR
	JSR ISET
	JSR	INITL
	JSR	EOUT
A5:	LDA	I,0
	STA	TIMER
	STA	TIMER+1
A1:	JSR	PLAY
	JSR	WAIT
	JSR CNTRL
	JSR OUTPT
	JSR	OBJ
	LDA	CREDIT
	BNE	A2
	INC	TIMER
	BNE	A1
	LDA	I,0
	STA	TIMER
	INC	TIMER+1
	LDA	TIMER+1
	CMP	I,6
	BNE	A1
	JMP	ATRACT
A2:	RTS
	.SBTTL	START ROUTINE
START:	LDA	I,0
	STA	HNUM
	STA PFLG
	LDA EFLG		;CHECK EXTENDED PLAY FLAGS
	CMP I,2			;IF 0, DO NOTHING
	BNE 10$			;IF 1, MAKE IT 2
	LDA I,0			;IF 2, RESET IT
	STA EFLG
	BEQ 15$
10$:	CMP I,1
	BNE 15$
	INC EFLG
15$:	LDA EFLG+1
	CMP I,2
	BNE 11$
	LDA I,0
	STA EFLG+1
	BEQ 12$
11$:	CMP I,1
	BNE 12$
	INC EFLG+1
12$:	JSR ISET
	JSR RSET
	JSR	INITL
	JSR	EOUT
	LDA	I,1
	STA	A,CLITE		;TURN ON 1-PLAYER START LITE
6$:	LDA	CREDIT
	CMP	I,2		;ENOUGH CREDIT FOR 2-PLAYER?
	BCC	1$		;NO
	LDA	I,1		;YES,TURN ON 2-PLAYER START LITE
	STA	A,CLITE2
1$:	LDA	A,SW
	AND	I,80		;START 1?
	BEQ	2$		;NO
	LDA	I,1		;YES,SET ATRK=1
	STA	ATRK
	DEC	CREDIT
	LDA I,0
	STA	A,CLITE2	;MAKE SURE 2-PLAY LITE OUT
	BPL	3$
2$:	LDA	A,SW
	AND	I,40		;START 2?
	BEQ	4$		;NO
	LDA	CREDIT		;YES,ENOUGH CREDIT?
	CMP	I,2
	BCC	4$		;NO
	LDA	I,2		;YES,SET ATRK=2
	STA	ATRK
	DEC	CREDIT
	DEC	CREDIT
	LDA	I,0
	STA	A,CLITE		;MAKE SURE 1-PLAY LITE OUT
	BPL	3$
4$:	JSR	WAIT		;LOOK FOR COINS
	JSR	CNTRL
	JSR	OUTPT
	JSR	HSCR
	JMP	6$
3$:	JSR ISET
	LDA	I,0
	STA	PFLG		;SET TO PLAYER 1
	STA	SCORE
	STA	SCORE+1
	STA	SCR2
	STA	SCR2+1
	STA EFLAG
	STA EFLAG+1
S6:	JSR RSET
	JSR	INITL
S5:	LDA	A,SB		;WAIT FOR SERVE BUTTON
	ASL
	BMI	S1
	JSR	CNTRL
	JSR	WAIT
	JSR	OUTPT
	JSR	HSCR
	LDA	FRAME		;FLASH PLAYER UP
	AND	I,10
	BEQ	1$
	LDA	I,0
	STA	A,SLITE
	LDA	I,1
	JSR	MESS
	LDA	ATRK		;IN 1-PLAYER GAME DO NOT
	CMP	I,1		;WRITE PLAYER-2 TO SCREEN
	BEQ	4$
	LDA	I,5
	BNE	2$
1$:	LDA	I,1
	STA	A,SLITE
	LDA	PFLG
	BNE	3$
	LDA	I,81
	BNE	2$
3$:	LDA	I,85
2$:	JSR	MESS
4$:	INC	FRAME
	BNE S5			;AUTO-SERVE
S1:	JSR	INITL
	LDA	I,0
	STA	A,ATR		;TURN OFF ATTRACT
	LDA I,0
	STA	A,SLITE
	LDA I,1
	STA SFLG1
S4:	JSR	PLAY
	LDA	DONE		;DONE WITH HEAT?
	BNE	S2		;YES
S200:	JSR 	WAIT		;NO,WAIT FOR VBLANK
	JSR	OUTPT		;UPDATE OBJECTS
	JSR	OBJ
	JSR	HSCR
	JMP	S4
S2:	JSR SAVE
	LDA I,0
	STA SFLG1
	LDA I,07F
	STA FRAME
8$:	JSR WAIT
	INC FRAME
	BNE 8$
	LDA ATRK
	CMP I,2
	BNE 26$
	LDA PFLG
	BNE 26$
	INC PFLG
7$:	JMP S6
26$:	LDA I,0
	STA PFLG
	INC HNUM
	LDA HNUM
	CMP HEATS
	BCC 7$
	LDA A,SB
	AND I,04
	BEQ S25
	LDY I,0
23$:	LDA AY,EFLG		;EXTENDED PLAY ALREADY AWARDED?
	BNE 20$			;YES
	CPY I,1			;NO, CHECK AND SEE
	BEQ 21$
	LDA EPLY+1		;PLAYER ONE
	CMP SCORE+1
	BCC 1$			;GOT IT
	BNE 20$
	LDA SCORE
	CMP EPLY
	BCS 1$
	BCC 20$
21$:	LDA EPLY+1
	CMP SCR2+1
	BCC 1$
	BNE 20$
	LDA SCR2
	CMP EPLY
	BCC 20$
1$:	INC CREDIT
	LDA I,1			;TURN ON APPROPRIATE
	STA A,CLITE		;CREDIT LITES
	LDA CREDIT
	CMP I,2
	BCC 9$
	LDA I,1
	STA A,CLITE2
9$:	LDA	I,088		;GOT EXTENDED PLAY
	STA	FRAME		;FLASH VIDEO
	LDA I,1
	STA AY,EFLG
	LDA I,32.
	STA SFLG3
	STY TEMP7
3$:	JSR	WAIT
	LDA	FRAME
	AND	I,4
	BEQ	4$
	LDA	I,1
4$:	STA	A,VINT
	STA	A,SOUND2
5$:	LDA 	I,0A
	JSR	MESS
	INC	FRAME
	BNE	3$
	LDA	I,8A
	JSR	MESS
	LDY TEMP7
20$:	INY
	CPY I,3
	BCC 23$
S25:	LDA	I,0		;NO, FREEZE SCREEN
	STA	FRAME
	STA SFLG1
	LDA	I,1
	STA	A,SLITE
1$:	JSR	WAIT
	LDA	FRAME		;FLASH GAME OVER
	AND	I,10
	BEQ	2$
	LDA	I,9
	BNE	3$
2$:	LDA	I,89
3$:	JSR	MESS
	LDA	FRAME
	CMP	I,30
	BCC	4$
	LDA	I,1		;ALLOW COINS TO COME IN
	STA	A,ATR		;DURING GAME OVER
4$:	INC	FRAME
	BNE	1$
	LDA	I,0
	STA	A,CLITE		;TURN OFF CREDIT LITES
	STA	A,CLITE2
	RTS
	.SBTTL	PLAY SUBROUTINE
PLAY:	LDA	TIME			;TIME TO DROP AN OBJECT?
	BEQ	P1			;YES
	DEC	TIME			;NO,DECREMENT TIME
	JMP	P50
P1:	LDA	TUP			;RESET TIME
	STA	TIME
P40:	LDX	I,6			;LOOK FOR AVAILABLE OBJECT
P3:	LDA	ZX,MODE
	BEQ	P4
	INX
	CPX	I,MAXOB
	BNE	P3
	JMP	P50			;NO AVAILABLE OBJECTS
P4:	JSR	RANDU			;PICK UP RANDOM HPOS
	AND	I,0F8
	STA	ZX,HPOS
	STA ZX,HPOST
	LDY ATRK
	BEQ 3$
	LDY LNUM
	CPY I,7
	BEQ 2$
	LSR
	LSR
	LSR
	TAY
	LDA AY,ARR
	BEQ P4
	LDA I,0
	STA AY,ARR
3$:	LDY LNUM
	LDA ATRK
	BNE 4$
	LDA I,73
	BNE 5$
2$:	LDY I,6
7$:	LDA AY,MODE
	BNE P50
	INY
	CPY I,MAXOB
	BNE 7$
	LDA TOG
	STA TEMP7
	JSR IISET
	JSR INITL
	LDA I,0
	LDX TEMP7
	CPX I,0
	BEQ 10$
	CPX I,1
	BEQ 11$
	STA MODE+1
11$:	STA MODE+2
10$:	STA MODE+3
	STA MODE+4
	STA MODE+5
	INX
	STX TOG
	JMP P1
4$:	LDA AY,SLART
5$:	STA ZX,VPOS
	STA ZX,VPOST
	LDY LNUM
	LDA AY,CODEL
	STA ZX,CODE
	STA ZX,CODET
	LDA AY,OBCH
	STA ZX,OCD
	STA ZX,OCDT
	LDA I,1
	STA ZX,MODE 		;ACTIVATE OBJECT
	STA ZX,MODET
	INC LOBJ
	LDA LOBJ
	CMP I,32.
	BCC P50
	LDA I,0
	STA LOBJ
	LDY I,0
	LDA I,1
6$:	STA AY,ARR
	INY
	CPY I,32.
	BNE 6$
	INC LNUM
	LDA LNUM
	CMP I,7
	BCC P50
	LDA I,7
	STA LNUM
P50:	JSR	CNTRL
	LDA	ATRK			;DO NOT INCREASE OBJECTS IN ATTRACT
	BEQ	U4
	LDA	FRAME			;EVERY ONCE IN
	AND	I,3F			;AWHILE DECREASE
	BNE	U4			;TIME BETWEEN OBJECTS
	LDA	TUP			;UP TO A MAXIMUM
	SEC
	SBC	I,6
	CMP	I,10
	BCS	U5
	LDA	I,10
U5:	STA	TUP
U4:	LDA FRAME
	AND I,0FF
	BNE 1$
	LDA ATRK		;IN GAME PLAY
	BEQ 2$			;WHILE FIRST TWO
	LDA MODE+5		;PADDLES ARE ACTIVE
	BNE 3$			;SLOW DOWN GAME
	LDA MODE+4
	BEQ 2$
3$:	LDA I,7F
	STA AMT
	LDA I,1
	STA AMT+1
	BNE 1$
2$:	LDA AMT
	CLC
	ADC I,60
	STA AMT
	LDA AMT+1
	ADC I,0
	STA AMT+1		;FIX UPPER SPEED
	CMP I,3
	BCC 1$
	LDA AMT
	CMP I,60
	BCC 1$
	LDA I,3
	STA AMT+1
	LDA I,60
	STA AMT
1$:	LDX I,6			;LOOK FOR ACTIVE OBJECTS
U2:	LDA ZX,MODE
	BEQ U1
	LDA FRAME
	AND I,1
	BNE U1
	LDA AX,VPOSL
	CLC
	ADC AMT
	STA AX,VPOSL
	LDA ZX,VPOS			;ACTIVE OBJECT,UPDATE POSITION
	ADC AMT+1
	BCC	U3
	LDA	I,0FF
U3:	STA	ZX,VPOS
U1:	INX
	CPX	I,MAXOB
	BNE	U2
	JSR	COLL			;LOOK FOR COLLISIONS
	LDA LNUM
	CMP I,2
	BNE P60
	LDA SFLG2
	CMP I,4
	BEQ 1$
	LDA SFLG3
	CMP I,4
	BEQ 1$
	LDA SFLG0
	CMP I,4
	BNE 2$
1$:	LDA I,2
	STA TARG
	LDA I,0
	STA MODE+5
2$:	JMP P100
P60:	CMP I,3
	BNE P61
	LDA SFLG2
	CMP I,4
	BEQ 2$
	LDA SFLG3
	CMP I,4
	BEQ 2$
	LDA SFLG0
	CMP I,4
	BNE P100
2$:	LDA I,3
	STA TARG
	LDA I,0
	STA MODE+4
	LDA I,2
	LDX I,0
1$:	STA ZX,CODE
	INX
	CPX I,6
	BNE 1$
	BEQ P100
P61:	CMP I,4
	BNE P62
	LDA SFLG2
	CMP I,4
	BEQ 1$
	LDA SFLG3
	CMP I,4
	BEQ 1$
	LDA SFLG0
	CMP I,4
	BNE P100
1$:	LDA I,4
	STA TARG
	LDA I,0
	STA MODE+3
	BEQ P100
P62:	CMP I,5
	BNE P63
	LDA SFLG2
	CMP I,4
	BEQ 2$
	LDA SFLG3
	CMP I,4
	BEQ 2$
	LDA SFLG0
	CMP I,4
	BNE P100
2$:	LDA I,5
	STA TARG
	LDA I,0
	STA MODE+2
	LDA I,4
	LDX I,0
1$:	STA ZX,CODE
	INX
	CPX I,6
	BNE 1$
	BEQ P100
P63:	CMP I,6
	BNE P100
	LDA SFLG2
	CMP I,4
	BEQ 1$
	LDA SFLG3
	CMP I,4
	BEQ 1$
	LDA SFLG0
	CMP I,4
	BNE P100
1$:	LDA I,6
	STA TARG
	LDA I,0
	STA MODE+1
P100:	INC	FRAME
	RTS
	.SBTTL	READ HORIZONTAL CONTROL
CNTRL:  LDA ATRK
	BEQ R5
	LDA A,HCON
	CMP I,40
	BCS 1$
	LDA I,40
	BNE 5$
1$:	CMP I,0B8
	BCC 5$
	LDA I,0B7
5$:	SEC
	SBC I,40
	ASL
R2:	LDX	I,5
R3:	STA	ZX,HPOS
	DEX
	BPL	R3
R4:	RTS
R5:	LDA I,0			;DELETE TOP 2 PADDLES
	STA MODE+5
	STA MODE+4
	LDX I,3
5$:	LDY I,6
3$:	LDA ZX,VPOS
	CMP AY,VPOS
	BCC 1$
	SEC
	SBC I,0A
	CMP AY,VPOS
	BCS 1$
	LDA AY,MODE
	BEQ 1$
	LDA ZX,HPOS
	CMP AY,HPOS
	BCS 2$
	LDA AY,HPOS
	SEC
	SBC ZX,HPOS
	CMP I,8
	BCC 9$
	LSR
	LSR
9$:	LSR
	CLC
	ADC ZX,HPOS
	JMP 4$
2$:	LDA ZX,HPOS
	SEC
	SBC AY,HPOS
	CMP I,8
	BCC 10$
	LSR
	LSR
10$:	LSR
	STA TEMP1
	LDA ZX,HPOS
	SEC
	SBC TEMP1
4$:	LDX I,5
8$:	STA ZX,HPOS
	DEX
	BPL 8$
	RTS
1$:	INY
	CPY I,MAXOB
	BNE 3$
	DEX
	BPL 5$
	RTS
	.SBTTL	COLLISION ROUTINE
COLL:	LDX	I,6
C17:	LDA	I,4
	SEC
	SBC	ZX,OCD
	STA	TEMP3			;SET CENTER OF OBJECT
	LDY ZX,OCD
	LDA	AY,LIMT
	STA	LIM
	LDA	AY,TABV
	STA	TEMP1+1
	LDA	CODE
	CMP	I,0
	BNE	C1
	LDA	AY,TAB1H
	BNE	C30
C1:	CMP	I,2
	BNE	C2
	LDA	AY,TAB2H
	BNE	C30
C2:	LDA	AY,TAB3H
C30:	STA	TEMP1
C3:	LDA	ZX,MODE			;OBJECT ACTIVE?
	BEQ	C4			;NO
	LDA	ZX,HPOS			;YES, CHECK IF
	CLC
	ADC	TEMP3
	SEC				;HORIZONTAL POSITION
	SBC	HPOS			;OF OBJECT IS WITHIN
	SEC
	SBC	I,8
	BPL	C5			;LIMITS OF PADDLE
	EOR	I,0FF
	CLC
	ADC	I,1
C5:	CMP	TEMP1
	BCC	C6
C10:	LDA	ZX,VPOS			;VERTICAL POSITION OF OBJECT=LIM?
	CMP	LIM
	BCC	C4			;NO
	LDA	ATRK
	BEQ	C32
	LDA	I,1
	STA	DONE			;SET DONE FLAG
	LDA	I,0
	LDX	I,6
C20:	STA	ZX,MODE
	INX
	CPX	I,MAXOB
	BNE	C20
	LDA I,32.
	STA SFLG3
	RTS
C32:	LDA	I,0
	STA	ZX,MODE
C4:	INX
	CPX	I,MAXOB
	BNE	C17
	RTS
C6:	LDY	I,0
C7:	LDA	AY,MODE
	BEQ	C12
	LDA	ZX,VPOS			;CHECK IF VERTICAL
	CLC
	ADC	TEMP3
	SEC				;POSITION OF OBJECT IS
	SBC	AY,VPOS			;WITHIN LIMITS OF ANY PADDLE
	SEC
	SBC	I,2
	BPL	C8
	EOR	I,0FF
	CLC
	ADC	I,1
C8:	CMP	TEMP1+1
	BCC	C9
C12:	INY
	CPY	I,6
	BNE	C7
	BEQ	C10
C9:	LDA	I,0
	STA	ZX,MODE			;DEACTIVATE OBJECT
	LDA ZX,HPOS
	LSR
	LSR
	LSR
	TAY
	LDA I,0
	STA AY,SAVM		;DEACTIVATE OBJECT IN
	INC LOBJS		;SAVM ARRAY
	LDA LOBJS		;DEACTIVATED ENTIRE LINE?
	CMP I,32.
	BCC 4$			;NO
	LDA I,0			;YES, GO TO NEXT LINE
	STA LOBJS
	LDY I,0
	LDA I,1
5$:	STA AY,SAVM
	INY
	CPY I,32.
	BNE 5$
	INC LNUMS
	LDA LNUMS
	CMP I,7
	BCC 4$
	LDA I,7
	STA LNUMS
4$:	LDA	ATRK
	BEQ	C4
	LDY I,4
	LDA ZX,CODE		;ATTACH SOUND TO
	CMP I,6			;A PARTICULAR KIND
	BNE 6$			;OF OBJECT
	STY SFLG2
	BEQ 8$
6$:	CMP I,0E
	BNE 7$
	STY SFLG3
	BEQ 8$
7$:	STY SFLG0
8$:	STX	TEMP5
	SED
	LDA	PFLG
	BEQ	1$
	LDA	TARG
	CLC
	ADC	SCR2
	STA	SCR2
	LDA	SCR2+1
	ADC	I,0
	STA	SCR2+1
	LDA A,SB
	AND I,04
	BEQ 2$
	LDA EFLAG+1		;EXTENDED SOUND GIVEN?
	BNE 2$			;YES
	LDA EFLAG+1		;NO, NO EXTENDED PLAY THIS GAME?
	BNE 2$			;CORRECT
	LDA EPLY+1		;NOW CHECK FOR EXTENDED PLAY
	CMP SCR2+1
	BCC 10$
	BNE 2$
	LDA SCR2
	CMP EPLY
	BCC 2$
10$:	LDA I,1
	STA EFLAG+1
	JMP 20$
1$:	LDA	TARG
	CLC
	ADC	SCORE
	STA	SCORE
	LDA	SCORE+1
	ADC	I,0
	STA	SCORE+1
	LDA A,SB
	AND I,04
	BEQ 2$
	LDA EFLAG
	BNE 2$
	LDA EFLG
	BNE 2$
	LDA EPLY+1
	CMP SCORE+1
	BCC 11$
	BNE 2$
	LDA SCORE
	CMP EPLY
	BCC 2$
11$:	LDA I,1
	STA EFLAG
20$:	LDA I,80
	STA SFLG4
2$:	CLD
	LDA	I,0
	JSR	NUM			;OUTPUT SCORE
	LDX	TEMP5
	JMP	C4
CODEL:  .BYTE	6,6,6,0E,0E,14,14
OBCH:	.BYTE	0,0,0,1,1,2,2
SLART:  .BYTE	40,40,38,32,2C,28,24
TAB1H:	.BYTE	12.,11.,10.		;LIMITS TABLES
TAB2H:	.BYTE	10.,10.,10.
TAB3H:	.BYTE	10.,10.,10.
TABV:	.BYTE	6,5,4
EOUT:	LDA	A,SW		;OUTPUT COIN MESSAGE
	AND	I,0C
	LSR
	LSR
	BEQ	5$
	ADC	I,5
	JSR	MESS
5$:	LDA A,SB
	AND I,04
	BEQ 8$
	LDA	I,12.		;OUTPUT EXTENDED PLAY MESSAGE
	JSR	MESS
	LDY	I,95
	LDA A,CSW		;ADJUST POINTER FOR
	AND I,20		;EXTENDED PLAY LEVEL
	BEQ 7$			;POSITION DEPENDENT ON
	LDY I,93		;LANGUAGE PICKED
	LDA A,CSW
	AND I,10
	BEQ 6$
	LDY I,92
	BNE 6$
7$:	LDA A,CSW
	AND I,10
	BEQ 6$
	LDY I,93
6$:	STY	TEMP2
	LDA	I,0D
	STA	TEMP2+1
	LDA	EPLY
	STA	PTR
	LDA	EPLY+1
	STA	PTR+1
	JSR	HEXN
8$:	RTS
	.SBTTL	INITIALIZATION ROUTINE
INITL:	LDA	I,0
	STA	DONE			;RESET DONE FLAG
	STA	TIME			;RESET TIME
	STA	FRAME
	STA AMT
	JSR	BLANK			;BLANK SCREEN
	LDA	I,0
	JSR	MESS			;OUTPUT MISSES MESSAGE
	LDA	I,1
	STA A,VINT
	JSR	MESS			;OUT SCORE MESSAGE
	LDA	ATRK			;IF JUST 1-PLAYER GAME,
	CMP	I,1			;DO NOT OUTPUT PLAYER-2 TO SCREEN
	BEQ	1$
	LDA	I,5
	JSR	MESS
1$:	LDA	I,11.
	JSR 	MESS
	LDA	PFLG
	STA	TEMP4
	LDA	I,0
	STA	PFLG
	LDA	I,0
	JSR	NUM			;OUTPUT ACTUAL SCORE
	LDA	ATRK
	CMP	I,1
	BEQ	2$
	LDA	I,1
	STA	PFLG
	LDA	I,0
	JSR	NUM
2$:	LDA	TEMP4
	STA	PFLG
	LDA	I,1
	JSR	NUM			;OUTPUT HEAT NUMBER
	JSR	HSCR
	LDA	A,SB			;SET # OF HEATS
	AND	I,08
	LSR
	LSR
	TAX
	CLC
	ADC I,3
	STA	HEATS		;SET # OF HEATS
	LDA	AX,RLEV		;SET REPLAY LEVEL
	STA	EPLY
	LDA	AX,RLEV+1
	STA	EPLY+1
	JSR	CNTRL
	LDA	I,243.
	STA	VPOS
	LDA	I,219.
	STA	VPOS+1
	LDA	I,195.
	STA	VPOS+2
	LDA	I,171.
	STA	VPOS+3
	LDA	I,147.
	STA	VPOS+4
	LDA	I,123.
	STA	VPOS+5
INIT2:	LDX 	I,6
I1:	LDA	I,0
	STA	ZX,HPOS		;SET OBJECTS HPOS,VPOS,CODE & MODE
	STA	ZX,VPOS
	STA ZX,VPOSL
	STA ZX,MODET
	INX
	CPX	I,MAXOB
	BNE	I1
	LDA	I,30		;SET TIME BETWEEN OBJECTS
	STA	TUP
	LDA	I,2
	STA	AMT+1		;SET RATE OF OBJECT FALL
	LDA	I,7F		;OUTPUT WALL AT TOP
	STA	PTR
	LDA	I,1F
	STA	PTR+1
	LDY	I,080
	LDA	I,0FF
1$:	STA	NY,PTR
	DEY
	BNE	1$
	JSR	COIN
	LDX I,14
	LDA I,80
	STA POS
	LDA I,0
	STA PTR
	LDA I,4
	STA POS+1
	LDA I,5
	STA PTR+1
3$:	LDA AX,PICT
	LDY I,0
	STA TEMP1
2$:	LDA LNUMS
	CMP I,6
	BNE 20$
	LDA AY,SAVM
	BEQ 21$
20$:	LDA TEMP1
	STA NY,POS
21$:	LDA LNUMS
	CMP I,5
	BCC 22$
	BEQ 30$
	BCS 23$
30$:	LDA AY,SAVM
	BEQ 23$
22$:	LDA TEMP1
	STA NY,PTR
23$:	STA A,TREST
10$:	INY
	CPY I,32.
	BNE 2$
	JSR UTIL
	INX
	CPX I,18
	BNE 3$
	LDA I,80
	STA POS
	LDA I,040
	STA PTR
	LDA I,5
	STA POS+1
	LDA I,6
	STA PTR+1
	LDX I,0E
4$:	LDA AX,PICT
	LDY I,0
	STA TEMP1
5$:	LDA LNUMS
	CMP I,4
	BCC 24$
	BEQ 31$
	BCS 25$
31$:	LDA AY,SAVM
	BEQ 25$
24$:	LDA TEMP1
	STA NY,POS
25$:	STA A,TREST
11$:	LDA LNUMS
	CMP I,3
	BCC 26$
	BEQ 32$
	BCS 12$
32$:	LDA AY,SAVM
	BEQ 12$
26$:	LDA TEMP1
	STA NY,PTR
12$:	INY
	CPY I,32.
	BNE 5$
	JSR UTIL
	INX
	CPX I,14
	BNE 4$
	LDA I,0
	STA PTR
	STA POS
	LDA I,7
	STA POS+1
	LDA I,8
	STA PTR+1
	LDX I,06
I3:	LDA AX,PICT
	LDY I,0
	STA TEMP1
7$:	LDA LNUMS
	CMP I,2
	BCC 27$
	BEQ 33$
	BCS 13$
33$:	LDA AY,SAVM
	BEQ 13$
27$:	LDA TEMP1
	STA NY,POS
13$:	LDA LNUMS
	CMP I,1
	BCC 28$
	BEQ 34$
	BCS 29$
34$:	LDA AY,SAVM
	BEQ 29$
28$:	LDA TEMP1
	STA NY,PTR
29$:	STA A,TREST
14$:	INY
	CPY I,32.
	BNE 7$
	JSR UTIL
	INX
	CPX I,0E
	BNE I3
	RTS
RLEV:	.WORD	450		;REPLAY LEVELS
	.WORD	750
UTIL:	LDA POS
	CLC
	ADC I,20
	STA POS
	LDA POS+1
	ADC I,0
	STA POS+1
	LDA PTR
	CLC
	ADC I,20
	STA PTR
	LDA PTR+1
	ADC I,0
	STA PTR+1
	RTS
	.SBTTL SAVE & RESTORE WALL STATUS
SAVE:	LDX I,0
	LDA PFLG
	BNE 1$
2$:	LDA ZX,CODE
	STA AX,SAVR1
	INX
	CPX I,3*MAXOB+5
	BNE 2$
	LDX I,0
3$:	LDA AX,SAVM
	STA AX,SAVS1
	INX
	CPX I,32.
	BNE 3$
	LDA LOBJS
	STA A,SAVS1+32.
	LDA LNUMS
	STA A,SAVS1+33.
	RTS
1$:	LDA ZX,CODE
	STA AX,SAVR2
	INX
	CPX I,3*MAXOB+5
	BNE 1$
	LDX I,0
4$:	LDA AX,SAVM
	STA AX,SAVS2
	INX
	CPX I,32.
	BNE 4$
	LDA LOBJS
	STA A,SAVS2+32.
	LDA LNUMS
	STA A,SAVS2+33.
	RTS
RSET:	LDX I,0
	LDA PFLG
	BNE 1$
2$:	LDA AX,SAVR1
	STA ZX,CODE
	INX
	CPX I,3*MAXOB+5
	BNE 2$
	LDX I,0
3$:	LDA AX,SAVS1
	STA AX,SAVM
	STA AX,ARR
	INX
	CPX I,32.
	BNE 3$
	LDA A,SAVS1+32.
	STA LOBJS
	STA LOBJ
	LDA A,SAVS1+33.
	STA LNUMS
	STA LNUM
	RTS
1$:	LDA AX,SAVR2
	STA ZX,CODE
	INX
	CPX I,3*MAXOB+5
	BNE 1$
	LDX I,0
4$:	LDA AX,SAVS2
	STA AX,SAVM
	STA AX,ARR
	INX
	CPX I,32.
	BNE 4$
	LDA A,SAVS2+32.
	STA LOBJS
	STA LOBJ
	LDA A,SAVS2+33.
	STA LNUMS
	STA LNUM
	RTS
ISET:	LDA I,0
	TAX
1$:	STA AX,SAVR1
	STA AX,SAVR2
	INX
	CPX I,3*MAXOB+5
	BNE 1$
	LDA I,1
	STA A,SAVR1+<3*MAXOB+1>
	STA A,SAVR2+<3*MAXOB+1>
	LDX I,0
2$:	STA AX,SAVR1+<2*MAXOB>
	STA AX,SAVR2+<2*MAXOB>
	INX
	CPX I,6
	BNE 2$
	LDA I,6
3$:	STA AX,SAVR1
	STA AX,SAVR2
	INX
	CPX I,MAXOB
	BNE 3$
	LDA PFLG
	STA TEMP7
	LDA I,0
	STA PFLG
	JSR IISET
	LDA I,1
	STA PFLG
	JSR IISET
	LDA TEMP7
	STA PFLG
	RTS
IISET:	LDA I,0
	TAX
1$:	STA ZX,CODE
	INX
	CPX I,3*MAXOB+5
	BNE 1$
	LDA I,1
	STA TARG
	LDX I,0
2$:	STA ZX,MODE
	STA A,TREST
	INX
	CPX I,6
	BNE 2$
	LDA I,6
3$:	STA ZX,CODE
	INX
	CPX I,MAXOB
	BNE 3$
	LDX I,0
	LDA I,1
4$:	STA AX,SAVM
	LDY PFLG
	BNE 5$
	STA AX,SAVS1
5$:	LDY PFLG
	BEQ 6$
	STA AX,SAVS2
6$:	STA AX,ARR
	INX
	CPX I,32.
	BNE 4$
	LDA I,0
	STA LOBJS
	STA LOBJ
	LDY PFLG
	BNE 7$
	STA A,SAVS1+32.
7$:	LDY PFLG
	BEQ 8$
	STA A,SAVS2+32.
8$:	LDA I,1
	STA LNUMS
	STA LNUM
	LDY PFLG
	BNE 9$
	STA A,SAVS1+33.
9$:	LDY PFLG
	BEQ 10$
	STA A,SAVS2+33.
10$:	RTS
	.SBTTL	HIGH SCORE ROUTINE
HSCR:	LDA	ATRK		;IF IN ATTRACT DO NOT
	BEQ	1$		;UPDATE HIGH SCORE
	LDA	SCRT+1
	CMP	SCORE+1
	BCC	2$
	BNE	3$
	LDA	SCRT
	CMP	SCORE
	BCS	3$
2$:	LDA	SCORE
	STA	SCRT
	LDA	SCORE+1
	STA	SCRT+1
3$:	LDA	SCRT+1
	CMP	SCR2+1
	BCC	4$
	BNE	1$
	LDA	SCRT
	CMP	SCR2
	BCS	1$
4$:	LDA	SCR2
	STA	SCRT
	LDA	SCR2+1
	STA	SCRT+1
1$:	LDA	I,5B		;ACTUALLY OUTPUT HIGH SCORE
	STA	TEMP2
	LDA	I,2
	STA	TEMP2+1
	LDY	I,4
6$:	LDA	I,20		;BLANK HIGH SCORE AREA
	JSR	STORE
	DEY
	BNE	6$
	LDA	SCRT
	STA	PTR
	LDA	SCRT+1
	STA	PTR+1
	JSR	HEXN
	RTS
HEXN:	LDY	I,0
	STY	TEMP1
	LDA	PTR+1
	AND	I,0F0
	LSR
	LSR
	LSR
	LSR
	BEQ	1$
	INC	TEMP1
	ORA	I,30
	JSR	STORE
	INY
1$:	LDA	PTR+1
	AND	I,0F
	BNE	2$
	LDX	TEMP1
	BEQ	3$
2$:	ORA	I,30
	INC	TEMP1
	JSR	STORE
	INY
3$:	LDA	PTR
	AND	I,0F0
	LSR
	LSR
	LSR
	LSR
	BNE	4$
	LDX	TEMP1
	BEQ	5$
4$:	ORA	I,30
	INC	TEMP1
	JSR	STORE
	INY
5$:	LDA	PTR
	AND	I,0F
	ORA	I,30
	JSR	STORE
	RTS
	.SBTTL	PADDLE OUTPUT ROUTINE
OUTPT:	LDY	CODE
	LDA	AY,PICT
	STA	PIK
	LDA	AY,PICT+1
	STA	PIK+1
	LDA	I,0
	STA	PIK+2
	LDA	HPOS
	AND	I,7
	TAY
	BEQ	L1
O42:	CLC
	ROR	PIK
	ROR	PIK+1
	ROR	PIK+2
	DEY
	BNE	O42
L1:	LDA	HPOST
	AND	I,0F8
	LSR
	LSR
	LSR
	STA	CNT
	LDA	HPOS
	AND	I,0F8
	LSR
	LSR
	LSR
	STA	VALU
	LDA	I,3
	STA	TEMP5
	LDA	I,0
	LDX	CNT
O48:	LDY	MODET+5
	BEQ	1$
	STA	AX,PAD6
	STA	AX,PAD6+20
	STA	AX,PAD6+40
	STA	AX,PAD6+60
1$:	LDY	MODET+4
	BEQ	2$
	STA	AX,PAD5
	STA	AX,PAD5+20
	STA	AX,PAD5+40
	STA	AX,PAD5+60
2$:	LDY	MODET+3
	BEQ	3$
	STA	AX,PAD4
	STA	AX,PAD4+20
	STA	AX,PAD4+40
	STA	AX,PAD4+60
3$:	LDY	MODET+2
	BEQ	4$
	STA	AX,PAD3
	STA	AX,PAD3+20
	STA	AX,PAD3+40
	STA	AX,PAD3+60
4$:	LDY	MODET+1
	BEQ	5$
	STA	AX,PAD2
	STA	AX,PAD2+20
	STA	AX,PAD2+40
	STA	AX,PAD2+60
5$:	STA	AX,PAD1
	STA	AX,PAD1+20
	STA	AX,PAD1+40
	STA	AX,PAD1+60
	INX
	DEC	TEMP5
	BNE	O48
	LDX	VALU
	LDA	I,3
	STA	TEMP5
	LDA	I,0
	STA	TEMP4
O51:	LDY	TEMP4
	LDA	AY,PIK
	LDY	MODE+5
	BEQ	1$
	STA	AX,PAD6
	STA	AX,PAD6+20
	STA	AX,PAD6+40
	STA	AX,PAD6+60
1$:	LDY	MODE+4
	BEQ	2$
	STA	AX,PAD5
	STA	AX,PAD5+20
	STA	AX,PAD5+40
	STA	AX,PAD5+60
2$:	LDY	MODE+3
	BEQ	3$
	STA	AX,PAD4
	STA	AX,PAD4+20
	STA	AX,PAD4+40
	STA	AX,PAD4+60
3$:	LDY	MODE+2
	BEQ	4$
	STA	AX,PAD3
	STA	AX,PAD3+20
	STA	AX,PAD3+40
	STA	AX,PAD3+60
4$:	LDY	MODE+1
	BEQ	5$
	STA	AX,PAD2
	STA	AX,PAD2+20
	STA	AX,PAD2+40
	STA	AX,PAD2+60
5$:	STA	AX,PAD1
	STA	AX,PAD1+20
	STA	AX,PAD1+40
	STA	AX,PAD1+60
	INX
	INC	TEMP4
	DEC	TEMP5
	BNE	O51
O50:	LDX	I,5
L3:	LDA	ZX,HPOS		;COPY BUFFERS
	STA	ZX,HPOST
	LDA	ZX,VPOS
	STA	ZX,VPOST
	LDA	ZX,MODE
	STA	ZX,MODET
	LDA	ZX,CODE
	STA	ZX,CODET
	DEX
	BPL	L3
	RTS
	.SBTTL	OBJECT OUTPUT ROUTINE
OBJ:	LDX	I,6
	STX	SNUM		;SET UP INDEX
B1:	LDX	SNUM
	LDA	ZX,MODET	;OBJECT ACTIVE?
	BEQ	B2		;NO
	LDA	ZX,HPOST	;YES
	STA	POS
	LDA	ZX,VPOST
	STA	POS+1
	LSR	POS+1
	ROR	POS
	LSR	POS+1
	ROR	POS
	LSR	POS+1
	ROR	POS
	LDY ZX,OCDT
	LDA	AY,MASK0
	STA	TEMP1
B2:	LDX	SNUM
	LDA	ZX,MODE
	BEQ	B3
	LDA	ZX,HPOS
	STA	PTR
	LDA	ZX,VPOS
	STA	PTR+1
	LSR	PTR+1
	ROR	PTR
	LSR	PTR+1
	ROR	PTR
	LSR	PTR+1
	ROR	PTR
	LDX	SNUM
	LDA	ZX,CODE
	STA	TEMP5
B3:	LDY	ZX,OCDT
	LDA	AY,LIN
	STA	TEMP2
	LDX	SNUM
	LDA	ZX,MODET
	BEQ	B5
	LDX	TEMP2
	LDY	I,0
B4:	LDA	NY,POS
	AND	TEMP1
	STA	NY,POS
	TYA
	CLC
	ADC	I,32.
	TAY
	DEX
	BNE	B4
B5:	LDX	SNUM
	LDA	ZX,MODE
	BEQ	B6
	LDY	ZX,OCD
	LDA	AY,LIN
	STA	TEMP2
	LDX	TEMP5
	LDY	I,0
B7:	LDA	NY,PTR
	ORA	AX,PICT
	STA	NY,PTR
	TYA
	CLC
	ADC	I,32.
	TAY
	INX
	DEC	TEMP2
	BNE	B7
B6:	INC	SNUM
	LDA	SNUM
	CMP	I,MAXOB
	BCS	B10
	JMP	B1
B10:	LDX	I,6
B9:	LDA	ZX,HPOS
	STA	ZX,HPOST
	LDA	ZX,VPOS
	STA	ZX,VPOST
	LDA	ZX,MODE
	STA	ZX,MODET
	LDA	ZX,CODE
	STA	ZX,CODET
	LDA ZX,OCD
	STA ZX,OCDT
	INX
	CPX	I,MAXOB
	BNE	B9
	RTS
PICT:	.BYTE	0FF,0FF		;FULL PADDLE, CODE=0
	.BYTE	03F,0FC		;3/4 PADDLE, CODE=2
	.BYTE	0F,0F0		;1/2 PADDLE, CODE=4
	.BYTE	0,1C,7E,7E	;LARGEST OBJECT, CODE=6
	.BYTE	0FE,0FE,0FC,38
	.BYTE	0,38,7C,0FC	;MEDIUM OBJECT, CODE=0E
	.BYTE	0F8,70
	.BYTE	0,70,0F0,0E0	;SMALL OBJECT, CODE=14
MASK0:	.BYTE	0,3,0F
LIN:	.BYTE	8,6,4
LIMT:	.BYTE	245.,247.,250.
	.SBTTL COIN SUBROUTINE
COIN:	NOP
CO6:	LDA COINS		;ANY COINS?
	BNE CO1			;YES
	LDA A,CSW		;NO,FREEPLAY?
	AND I,0C
	BNE CO10
CO2:	LDA I,0			;YES, SET COINS=0 AND
	STA COINS		;CREDIT=2
	LDA I,2
	STA CREDIT
CO10:	RTS
CO1:	LDA A,CSW		;COIN MODE?
	AND I,0C
	BEQ CO2			;FREE/DAY
	AND I,8
	BEQ CO5			;/COIN/CREDIT
	LDA A,CSW
	AND I,4
	BEQ CO4
	INC CREDIT		;1 COIN 2 CREDITS
	BPL CO5
CO4:	LDA COINS		;2 COINS 1 CREDIT
	CMP I,2
	BCC CO10
	DEC COINS
CO5:	DEC COINS
	INC CREDIT
	LDA CREDIT		;LIMIT CREDIT
	CMP I,0F		;ACCUMULATION TO 15
	BCC CO6
	LDA I,0F
	STA CREDIT
	BNE CO6
	.SBTTL MESSAGE ROUTINE
;
;MESSAGE ROUTINE
;	ON ENTRY A-REG	=BITS 0-5 = # OF MESSAGE
;
;			=BIT 7    = 1 BLANK MESSAGE
;				    0 WRITE MESSAGE
;
MESS:	LDX I,0
	STX PTR
	STX PTR+1
	CLC
	ROL
	ROL PTR			;SAVE R/W BIT
	TAX
	LDA A,CSW
	AND I,20
	BEQ 1$
	LDA A,CSW
	AND I,10
	BEQ 2$
	LDA AX,MESGG		;GERMAN
	STA TEMP1
	LDA AX,MESGG+1
	STA TEMP1+1
	LDA AX,POSMG
	STA TEMP2
	LDA AX,POSMG+1
	BNE 3$
2$:	LDA AX,MESGF		;FRENCH
	STA TEMP1
	LDA AX,MESGF+1
	STA TEMP1+1
	LDA AX,POSMF
	STA TEMP2
	LDA AX,POSMF+1
	BNE 3$
1$:	LDA A,CSW
	AND I,10
	BEQ 4$
	LDA AX,MESGS		;SPANISH
	STA TEMP1
	LDA AX,MESGS+1
	STA TEMP1+1
	LDA AX,POSMS
	STA TEMP2
	LDA AX,POSMS+1
	BNE 3$
4$:	LDA AX,MESG		;ENGLISH
	STA TEMP1
	LDA AX,MESG+1
	STA TEMP1+1
	LDA AX,POSM
	STA TEMP2
	LDA AX,POSM+1
3$:	STA TEMP2+1
M40:	LDY I,0
M1:	LDA NY,TEMP1
	BEQ M5
	AND I,3F
	LDX PTR
	BEQ M2
	LDA I,20
M2:	JSR STORE
	INY
	BPL M1
M5:	RTS
BLANK:	LDA	I,2		;ROUTINE BLANKS SCREEN
	STA	POS+1
	LDA	I,0
	STA	POS
	LDX	I,1E
2$:	LDY	I,0
	TYA
1$:	STA	NY,POS
	INY	
	BNE	1$
	STA	A,TREST		;PULSE WATCHDOG
	INC	POS+1
	DEX
	BNE	2$
	RTS
	.SBTTL WAIT SUBROUTINE
WAIT:	LDA A,AMODE
	BMI WAIT
2$:	LDA A,AMODE
	BPL 2$
	LDA A,AMODE
	ROL
	ROL
	BPL 1$
	JSR ITEST
1$:	STA A,TREST		;PULSE WATCHDOG
	JSR COIN
	JSR RANDU
	JSR SOUND
	RTS
	.SBTTL OUTPUT A NUMBER ROUTINE
NUM:	BNE N1			;HEAT NUMBER OR SCORE?
	LDA I,7C
	LDX PFLG
	BNE 1$
	LDA I,6B		;SCORE, SET UP INDIRECT
1$:	STA TEMP2		;TO SCREEN
	LDA I,3
	STA TEMP2+1
	LDA PFLG
	BNE 2$
	LDA SCORE
	STA PTR
	LDA SCORE+1
	STA PTR+1
	JMP 3$
2$:	LDA SCR2
	STA PTR
	LDA SCR2+1
	STA PTR+1
3$:	JSR HEXN
	RTS
N1:	LDA I,49
	STA TEMP2
	LDA I,2
	STA TEMP2+1
	LDY I,0
	LDA HNUM		;GET HEAT NUMBER
	ORA I,30
	JSR STORE		;OUTPUT TO SCREEN
	RTS
	.SBTTL STORE CHARACTER SUBROUTINE
STORE:	TAX			;ROUTINE TO OUTPUT 1 CHARACTER
	LDA TEMP2
	STA POS
	LDA TEMP2+1
	STA POS+1
	LDA I,0
	STA TEMP3
ST1:	LDA TEMP3
	BNE ST2
	LDA AX,MTAB1
	JMP ST10
ST2:	CMP I,1
	BNE ST3
	LDA AX,MTAB2
	JMP ST10
ST3:	CMP I,2
	BNE ST4
	LDA AX,MTAB3
	JMP ST10
ST4:	CMP I,3
	BNE ST5
	LDA AX,MTAB4
	JMP ST10
ST5:	CMP I,4
	BNE ST6
	LDA AX,MTAB5
	JMP ST10
ST6:	CMP I,5
	BNE ST7
	LDA AX,MTAB6
	JMP ST10
ST7:	LDA AX,MTAB7
ST10:	STA NY,POS
	LDA POS
	CLC
	ADC I,32.
	STA POS
	LDA POS+1
	ADC I,0
	STA POS+1
	INC TEMP3
	LDA TEMP3
	CMP I,7
	BNE ST1
	RTS
	.SBTTL SOUND CONTROL
SOUND:	LDA ATRK
	BEQ 4$
	LDA SFLG1
	BEQ 4$
	STA A,SCON
	LDA FRAME
	AND I,1F
	BNE 3$
	INC SFLG1
	LDA SFLG1
	CMP I,3F
	BCC 3$
	LDA I,3F
	STA SFLG1
	BNE 3$
4$:	STA A,SCON
3$:	LDA SFLG4		;HAD NEW SOUND
	BEQ 6$
	DEC SFLG4
	LDA SFLG4
	AND I,8
	BNE 7$
	LDA I,0
	STA A,SOUND3
	LDA I,1
	STA A,SOUND2
	BNE 8$
7$:	LDA I,1
	STA A,SOUND3
	LDA I,0
	STA A,SOUND2
	BEQ 8$
6$:	LDA SFLG2
	BEQ 1$
	DEC SFLG2
	LDA I,1
1$:	STA A,SOUND2
	LDA SFLG3
	BEQ 2$
	DEC SFLG3
	LDA I,1
2$:	STA A,SOUND3
8$:	LDA SFLG0
	BEQ 5$
	DEC SFLG0
	LDA I,1
5$:	STA A,SOUND1
	RTS
	.SBTTL PWRON INITIALIZATION ROUTINE
SETUP:	LDA I,0
	STA COINS
	STA CREDIT
	STA HNUM
	STA SCORE
	STA SCORE+1
	STA SCR2
	STA SCR2+1
	STA SCRT
	STA SCRT+1
	STA SFLG0
	STA SFLG1
	STA SFLG2
	STA SFLG3
	STA SFLG4
	STA EFLG
	STA EFLG+1
	STA KOIN1
	STA KOIN1+1
	STA FFLG
	STA FFLG+1
	STA A,SOUND1
	STA A,SOUND2
	STA A,SOUND3
	STA A,CLITE		;TURN OFF CREDIT LITES
	STA A,CLITE2
	LDA I,1
	STA A,SLITE
	STA A,VINT
	LDA A,HCON
	LDX I,5
1$:	STA ZX,HPOS
	DEX
	BPL 1$
	RTS
	.SBTTL INTERNAL SELF-TEST
ITEST:	JSR BLANK
	LDA I,1
	STA A,VINT
2$:	LDA I,0FF		;CHECK OUT 1ST
	STA AX,0		;256 BYTES
	LDA AX,0
	CMP I,0FF
	BNE 1$
	STA A,TREST
	INX
	BNE 2$
3$:	LDA I,0FF		;CHECK OUT 2ND 256 BYTES
	STA AX,100
	LDA AX,100
	CMP I,0FF
	BNE 1$
	STA A,TREST
	INX
	BNE 3$
	STX TEMP1		;NOW CHECK OUT REST OF RAM
	LDA I,2
	STA TEMP1+1
5$:	LDY I,0
	LDA I,0FF
	STA NY,TEMP1
	STA A,TREST
	LDA NY,TEMP1
	CMP I,0FF
	BNE 1$
	LDA TEMP1+1
	CMP I,1F
	BNE 7$
	LDA TEMP1
	CMP I,0FF
	BEQ 8$
7$:	LDY I,1
	LDA NY,TEMP1
	BNE 1$
8$:	INC TEMP1
	BNE 5$
	INC TEMP1+1
	LDA TEMP1+1
	CMP I,20
	BNE 5$
	JSR FREEZE
	JSR BLANK
	JSR FREEZE
	LDA I,2
	BNE 9$
1$:	JSR BLANK
	LDA I,3
9$:	JSR MESS
I5:	LDA I,4			;ROM CHECKSUM
	JSR MESS		;FIRST OUTPUT ROM OK TO SCREEN
	LDA I,30
	STA TEMP2
	LDA I,6
	STA TEMP2+1
	LDA I,PWRON&0FF00/256.
	STA TEMP5+1
	LDX I,0
I8:	LDA I,4
	STA TEMP1
	LDA I,PWRON&0FF
	STA TEMP5
	TAY
I7:	CLC
	ADC NY,TEMP5		;BUILD CHECKSUM
	INY
	STA A,TREST		;PULSE WATCHDOG
	BNE I7
	INC TEMP5+1
	DEC TEMP1
	BNE I7
	CMP AX,CKSUM		;CHECKSUM OK?
	BEQ I9			;YES
	STX TEMP6
	TXA			;BAD ROM CHECKSUM
	ORA I,30		;OUTPUT ROM # AND A BLANK
	JSR STORE
	INC TEMP2
	LDA I,20
	JSR STORE
	LDX TEMP6
I9:	INX
	CPX I,6
	BNE I8
	LDA I,0
	STA SFLG0
	STA SFLG1
	STA SFLG2
	STA SFLG3
	STA SFLG4
	STA A,SCON
	STA A,ATR		;TURN ON SOUND
	STA A,SLITE
	LDA I,1
	STA A,CLITE
	STA A,CLITE2
	LDA I,4B
	STA TEMP2
	LDA I,7
	STA TEMP2+1
I10:	LDY I,0
	LDA A,HCON		;READ HORIZONTAL CONTROL
	AND I,0F0		;AND OUTPUT TO
	LSR			;SCREEN IN HEX
	LSR
	LSR
	LSR
	JSR ASK
	JSR STORE
	INY
	LDA A,HCON
	AND I,0F
	JSR ASK
	JSR STORE
	DEY
	LDA A,AMODE		;CHECK COIN SWITCHES
	ROR
	BCC 1$
	LDA I,3
	STA SFLG3
1$:	ROR
	BCC 2$
	LDA I,3
	STA SFLG3
2$:	LDA A,AMODE		;CHECK SERVE SWITCH
	ROL
	BPL 3$
	LDA I,3
	STA SFLG3
3$:	LDA A,CSW		;CHECK START SWITCHES
	BPL 4$
	LDA I,3
	STA SFLG3
4$:	LDA A,CSW
	ROL
	BPL 5$
	LDA I,3
	STA SFLG3
5$:	JSR SOUND
6$:	LDA A,AMODE
	BMI 6$
7$:	LDA A,AMODE
	BPL 7$
	STA A,TREST		;PULSE WATCHDOG
10$:	LDA A,AMODE
	BMI 10$
11$:	LDA A,AMODE
	BPL 11$
	LDA A,AMODE		;STILL IN SELF-TEST?
	ROL
	ROL
	BMI I10			;YES
	JMP PWRON		;NO, RETURN
CKSUM:	.BYTE 65,34,2D,0,30,09,0E5	;CHECKSUM TABLE
FREEZE:	LDA I,0			;WAITS AROUND AWHILE
	TAX
	TAY
FZ1:	INX
	BNE FZ1
	INY
	STA A,TREST		;PULSE WATCHDOG
	BNE FZ1
	CLC
	ADC I,1
	CMP I,2
	BCC FZ1
	RTS
ASK:	CMP I,0A		;CONVERT A-REG TO ASCII
	BCS AK1
	ORA I,30
	RTS
AK1:	SEC
	SBC I,9
	RTS
	.SBTTL RANDOM NUMBER GENERATOR
RANDU:  CLC
	LDA POLY
	BIT A,MM5
	BEQ 1$
	SEC
1$:	BIT A,MM2
	BEQ 3$
	BCS 2$
	SEC
	BCS 3$
2$:	CLC
3$:	AND I,0F
	BNE 4$
	ROL
	EOR I,1
	LSR
4$:	ROL
	STA POLY
	ASL
	ASL
	ASL
	RTS
MM5:	.BYTE 10
MM2:	.BYTE 2
	.SBTTL NMI PROGRAM PROTECT LOGIC
NMI:	PHA
	TXA
	PHA
	TYA
	PHA
	CLD
	LDA A,AMODE		;IN SELF-TEST?
	AND I,20
	BNE NM1			;YES, IGNORE PROGRAM PROTECT LOGIC
	LDA RTEST		;CHECK RAM CELLS
	CMP I,92
	BNE NM2
	LDA RTEST+1
	CMP I,3F
	BNE NM2
	TSX
	LDA AX,106		;GET PCH
	CMP I,PWRON&0FF00/256.	;VALID RANGE?
	BCC NM3
	CMP I,PWRON+1800&0FF00/256.
	BCS NM3
	LDA A,COINAD
	LDX I,0
6$:	ROR
	ROR ZX,KOIN1
	LDY ZX,KOIN1
	BNE 7$
	LDY ZX,FFLG
	BEQ 8$
	INC COINS
	LDY I,0
	STY ZX,FFLG
	BEQ 8$
7$:	CPY I,07F
	BNE 8$
	LDY I,1
	STY ZX,FFLG
8$:	INX
	CPX I,2
	BNE 6$
	LDA A,AMODE
	AND I,10
	BNE NM1
	LDA I,0
	STA KOIN1
	STA KOIN1+1
	STA FFLG
	STA FFLG+1
NM1:	PLA
	TAY
	PLA
	TAX
	PLA
	RTI
NM2:	LDA I,92
	STA RTEST
	LDA I,3F
	STA RTEST+1
NM3:	JMP PWRON
	.SBTTL	ASCII CODE CONVERSION TABLES
MTAB1:	.BYTE 0,38,0FC,3C,0F8,0FC,0FE,3E,0C6
	.BYTE 0FC,6,0C6,0C0,0C6,0C6,7C,0FC
	.BYTE 7C,0FC,78,0FC,0C6,0C6,0C6,0C6
	.BYTE 0CC,0FE,0,0,0,0,0,0
	.BYTE 0,0,0,0,0,0,0,0,0,0
	.BYTE 0,0,0,0,0,38,30,7C
	.BYTE 7E,1C,0FC,3C,0FE,78,7C
MTAB2:	.BYTE 0,6C,0C6,66,0CC,0C0,0C0,60,0C6
	.BYTE 30,6,0CC,0C0,0EE,0E6,0C6,0C6
	.BYTE 0C6,0C6,0CC,30,0C6,0C6,0C6,0EE
	.BYTE 0CC,0E,0,0,0,0,0,0
	.BYTE 0,0,0,0,0,0,0,0,0,0
	.BYTE 0,0,0,0,0,4C,70,0C6
	.BYTE 0C,3C,0C0,060,0C6,0C4,0C6
MTAB3:	.BYTE 0,0C6,0C6,0C0,0C6,0C0,0C0,0C0,0C6
	.BYTE 030,6,0D8,0C0,0FE,0F6,0C6,0C6
	.BYTE 0C6,0C6,0C0,030,0C6,0C6,0D6,7C
	.BYTE 0CC,1C,0,0,0,0,0,0
	.BYTE 0,0,0,0,0,0,0,0,0,0
	.BYTE 0,0,0,0,0,0C6,030,0E
	.BYTE 18,6C,0FC,0C0,0C,0E4,0C6
MTAB4:	.BYTE 0,0C6,0FC,0C0,0C6,0F8,0FC,0CE,0FE
	.BYTE 30,6,0F0,0C0,0FE,0FE,0C6,0C6
	.BYTE 0C6,0CE,07C,30,0C6,0EE,0FE,38
	.BYTE 78,38,0,0,0,0,0,0
	.BYTE 0,0,0,0,0,0,0,0,0,0
	.BYTE 0,0,7C,0,0,0C6,030,3C
	.BYTE 3C,0CC,6,0FC,018,78,7E
MTAB5:	.BYTE 0,0FE,0C6,0C0,0C6,0C0,0C0,0C6,0C6
	.BYTE 30,6,0F8,0C0,0D6,0DE,0C6,0FC
	.BYTE 0DE,0F8,6,30,0C6,7C,0FE,7C
	.BYTE 30,70,0,0,0,0,0,0
	.BYTE 0,0,0,0,0,0,0,0,0,0
	.BYTE 0,0,7C,0,0,0C6,30,78
	.BYTE 6,0FE,6,0C6,30,9E,6
MTAB6:	.BYTE 0,0C6,0C6,66,0CC,0C0,0C0,66,0C6
	.BYTE 30,0C6,0DC,0C0,0C6,0CE,0C6,0C0
	.BYTE 0CC,0DC,0C6,30,0C6,38,0EE,0EE
	.BYTE 30,0E0,0,0,0,0,0,0
	.BYTE 0,0,0,0,0,0,0,0,0,0
	.BYTE 0,0,0,0,0,64,30,0E0
	.BYTE 0C6,0C,0C6,0C6,30,86,0C
MTAB7:	.BYTE 0,0C6,0FC,3C,0F8,0FE,0C0,03E,0C6
	.BYTE 0FC,7C,0CE,0FE,0C6,0C6,7C,0C0
	.BYTE 7A,0CE,7C,30,7C,10,0C6,0C6
	.BYTE 30,0FE,0,0,0,0,0,0
	.BYTE 0,0,0,0,0,0,0,0,0,0
	.BYTE 0,0,0,0,0,38,0FC,0FE
	.BYTE 7C,0C,7C,7C,30,7C,78
	.SBTTL	MESSAGE TABLES
MESG:	.WORD	MES0
	.WORD	MES1
	.WORD	MES2
	.WORD	MES3
	.WORD	MES4
	.WORD	MES5
	.WORD	MES6
	.WORD	MES7
	.WORD	MES8
	.WORD	MES9
	.WORD	MES10
	.WORD	MES11
	.WORD	MES12
POSM:	.WORD	241
	.WORD	361
	.WORD	50B
	.WORD	50B
	.WORD	62B
	.WORD	372
	.WORD	0A08
	.WORD	0A08
	.WORD	0A08
	.WORD	0A0B
	.WORD	0D89
	.WORD	250
	.WORD	0D83
MES0:	.ASCIZ	/MISSES/
MES1:	.ASCIZ	/PLAYER-1/
MES2:	.ASCIZ	/RAM  OK/
MES3:	.ASCIZ	/RAM  BAD/
MES4:	.ASCIZ	/ROM  OK/
MES5:	.ASCIZ	/PLAYER-2/
MES6:	.ASCIZ	/1 COIN 1 PLAYER /
MES8:	.ASCIZ	/1 COIN 2 PLAYERS/
MES7:	.ASCIZ	/2 COINS 1 PLAYER/
MES9:	.ASCIZ	/GAME OVER/
MES10:	.ASCIZ	/EXTENDED PLAY/
MES11:	.ASCIZ	/HIGH SCORE/
MES12:	.ASCIZ	/EXTENDED PLAY FOR      PTS/
MESGG:  .WORD MES0G
	.WORD MES1G
	.WORD MES2
	.WORD MES3
	.WORD MES4
	.WORD MES5G
	.WORD MES6G
	.WORD MES7G
	.WORD MES8G
	.WORD MES9G
	.WORD MES10G
	.WORD MES11G
	.WORD MES12G
POSMG:	.WORD 241
	.WORD 361
	.WORD 50B
	.WORD 50B
	.WORD 62B
	.WORD 372
	.WORD 0A07
	.WORD 0A07
	.WORD 0A07
	.WORD 0A0B
	.WORD 0D85
	.WORD 24F
	.WORD 0D81
MES0G:	.ASCIZ	/FEHLER/
MES1G:	.ASCIZ	/SPIELER-1/
MES5G:	.ASCIZ	/SPIELER-2/
MES6G:	.ASCIZ	/1 MUENZE 1 SPIELER/
MES7G:	.ASCIZ	/2 MUENZE 1 SPIELER/
MES8G:	.ASCIZ	/1 MUENZE 2 SPIELER/
MES9G:	.ASCIZ	/SPIELENDE/
MES10G:	.ASCIZ	/VERLAENGERTE SPIELZEIT/
MES11G:	.ASCIZ	/TOP TREFFER/
MES12G:	.ASCIZ	/VERLAENGERT FUER      PUNKTEN/
MESGF:	.WORD MES0F
	.WORD MES1F
	.WORD MES2
	.WORD MES3
	.WORD MES4
	.WORD MES5F
	.WORD MES6F
	.WORD MES7F
	.WORD MES8F
	.WORD MES9F
	.WORD MES10F
	.WORD MES11F
	.WORD MES12F
POSMF:	.WORD 241
	.WORD 361
	.WORD 50B
	.WORD 50B
	.WORD 62B
	.WORD 372
	.WORD 0A08
	.WORD 0A08
	.WORD 0A08
	.WORD 0A09
	.WORD 0D8A
	.WORD 24C
	.WORD 0D83
MES0F:	.ASCIZ	/ERREURS/
MES1F:	.ASCIZ	/JOUEUR-1/
MES5F:	.ASCIZ	/JOUEUR-2/
MES6F:	.ASCIZ	/1 PIECE 1 JOUEUR/
MES7F:	.ASCIZ	/2 PIECES 1 JOUEUR/
MES8F:	.ASCIZ	/1 PIECE 2 JOUEURS/
MES9F:	.ASCIZ	/FIN DE PARTIE/
MES10F:	.ASCIZ	/JEU PROLONGE/
MES11F:	.ASCIZ	/MEILLEUR SCORE/
MES12F:	.ASCIZ	/JEU PROLONGE DE      POINTS/
MESGS:	.WORD MES0S
	.WORD MES1S
	.WORD MES2
	.WORD MES3
	.WORD MES4
	.WORD MES5S
	.WORD MES6S
	.WORD MES7S
	.WORD MES8S
	.WORD MES9S
	.WORD MES10S
	.WORD MES11S
	.WORD MES12S
POSMS:	.WORD 241
	.WORD 361
	.WORD 50B
	.WORD 50B
	.WORD 62B
	.WORD 372
	.WORD 0A07
	.WORD 0A07
	.WORD 0A07
	.WORD 0A0A
	.WORD 0D89
	.WORD 24E
	.WORD 0D83
MES0S:	.ASCIZ /FALLOS/
MES1S:	.ASCIZ /JUGADOR-1/
MES5S:	.ASCIZ /JUGADOR-2/
MES6S:	.ASCIZ /1 MONEDA 1 JUGADOR/
MES8S:	.ASCIZ /1 MONEDA 2 JUGADORES/
MES7S:	.ASCIZ /2 MONEDAS 1 JUGADOR/
MES9S:	.ASCIZ /FIN PARTIDA/
MES10S:	.ASCIZ /JUGADA EXTRA/
MES11S:	.ASCIZ /PUNTAJE ALTO/
MES12S:	.ASCIZ /JUGADA EXTRA DE      PUNTOS/
	.REPT	PWRON+800+0FFA-.
	.BYTE	0
	.ENDM
	.=PWRON+800+0FFA
	.WORD	NMI		;VECTORS
	.WORD	PWRON
	.WORD	PWRON
	.=0F7C3
	.WORD NMI
	.END
                                                                                                                                                                                         